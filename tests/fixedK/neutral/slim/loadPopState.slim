// set up a simple neutral simulation
initialize() {
	if (!exists("slimgui")) {
		defineConstant("seed", getSeed());
	}
	else {
		setSeed(asInteger(round(runif(1, 1, 2^62 - 1))));
		defineConstant("seed", getSeed());
		catn("Actual seed: " + asInteger(seed));
	}
	
	
	defineConstant("wd", "/mnt/c/GitHub/SLiMTests/tests/indTrack/slim/");
	// Load functions from file
	source(paste0(wd, "netFns.slim"));
	setCfgParam("debug", T); // Verbose debug outputs straight to stdout
	
	// Declare/define parameters if they haven't been by the command line arguments
	// Values from literature are cited
	// The realistic range of w^2 is 3-50, so 18 to 5000 for 2w^2, or 0.05556 to 0.0002 for 1/2w^2
	setCfgParam("popPath", "/mnt/d/SLiMTests/tests/equalK_sweep/data/pop_states/slim_popstate13_32.bin");
	setCfgParam("mu", 9.1528e-6); // Mutation rate averaged across two samples of A. thaliana from Table 1 Aston et al. 2017, transformed to per locus by multiplying by 1346 (Xu et al. 2006) 
	setCfgParam("N", 5000); // Population size
	setCfgParam("N_sires", 5); // Number of sires for sampling
	setCfgParam("N_dam", 20); // Number of dams per sire
	setCfgParam("N_perdam", 10); // Number of offspring per dam
	setCfgParam("N_samp", N_sires*N_dam*N_perdam); // Sample size for haplotypes etc.
	setCfgParam("del_mean", -1); // Mean used for the gamma distribution that deleterious mutations are pulled from
	setCfgParam("del_shape", 10); // Shape parameter for gamma distribution used for deleterious mutations
	setCfgParam("rwide", 0.5); // Whole genome recombination rate
	setCfgParam("nloci", 8); // Number of U and E sites along the chromosome
	setCfgParam("ueProp", 0.5); // Proportion of Ubiquitinylation regulators vs enhancers among the n loci
	setCfgParam("genomelength", 1000); // Loci across the genome - some are deleterious, others QTL, others neutral
	setCfgParam("locimu", 0.0); // Mean used for QTL mutation effect distribution pulls
	setCfgParam("locisigma", 1.0); // Variance used for QTL effect pulls - the effect size distribution
	setCfgParam("locidist", paste0("exp(rnorm(1,", locimu, ",", locisigma, "));"));
	setCfgParam("con_props", c(1.0, 0.0, 0.0)); // Proportion of genes that have low/medium/high substitution rate
	setCfgParam("constraints", matrix(c(1.0, 0.0, 1.0, 1.0, 1.0, 1.0,
		5.0, 10.0, 1.0, 1.0, 1.0, 1.0,
		50.0, 100.0, 1.0, 1.0, 1.0, 1.0),
		ncol = 6, byrow = T)); // Matrix of lambdas: m1/m2/m3/m4/m5/m6 weights in columns, rows are low/medium/high sub rate
	
	setCfgParam("width", 0.05);	// Set so 1 phenotypic unit deviation is a 20% drop in fitness
	// TODO: Set this to a large value, adjust width also to have a longer adaptation period: measure the distribution of effects for adaptation!!!!	
	setCfgParam("optBurn", 1); // Optimum during burn-in	
	setCfgParam("optShift", 1); // How far away optimum phenotype should be from mean phenotype post-burnin: Kingsolver et al. 2001 suggests .01 to 10 phenotypic units
	
	setCfgParam("printH", F); // Print values of heterozygosity over time during burn-in: used to decide how long burn-in needs to be
	setCfgParam("burnTime", 50000); // Number of generations of neutral burn-in before the test period
	setCfgParam("testTime", 2000); // Number of generations of test time: where selection is applied (or additional generations of drift, if selType = "d")
	setCfgParam("samplerate", c(500, 500)); // Sample rate in generations for phenotypic output (first value) and allelic output (second value)
	setCfgParam("molTraitFix", -1); // Molecular trait to hold fixed (disable mutations): 0 = aZ, 1 = bZ, 2 = KZ, 3 = KXZ
	setCfgParam("testSampling", 50); // Sample rate during test period
	setCfgParam("adaptiveSampling", F); // Enable adaptive sampling rates to sample more often when phenotypes are changing rapidly. 
	setCfgParam("sampleLimits", c(0.1*samplerate, 5*samplerate)); // Set maximum and minimum sample rates to adjust between 
	
	setCfgParam("modelindex", 1); // Identifier for the combination of predictors used in latin hypercube: this is the row number 
	// in the lscombos.csv file
	setCfgParam("identifier", paste(asString(seed), modelindex, sep = "_")); // Unique identifier for the run - seed_modelindex
	
	// Set up the filesystem for saving data - thread safety, write to separate files for each run with unique names
	//	We write to /$PBS_JOBFS and copy all files of the same type after the full job is complete to a combined file
	// IMPORTANT: in the PBS script remember to cd $PBS_JOBFS
	
	setCfgParam("outPositions", paste0('slim_pos', identifier, '.csv')); // Output filename/path for locus positions
	setCfgParam("outQG", paste0('slim_qg', identifier, '.csv')); // Output filename/path for the trait means/variance
	setCfgParam("outOpt", paste0('slim_opt', identifier, '.csv')); // Output filename/path for the trait optimum values for each run
	setCfgParam("outMuts", paste0('slim_muts', identifier, '.csv')); //Output filename/path for the mutation information
	setCfgParam("outDict", paste0('slim_dict', identifier, '.csv')); // Output filename/path for dictionary of genetic constraints
	setCfgParam("outLD", c(paste0('slim_ld_burnin', identifier, '.tsv'),
		paste0('slim_ld_final', identifier, '.tsv'))); // Output filename/path for LD matrix (only generate one!)
	setCfgParam("outODEPars", paste0('slim_medodepar', identifier, '.csv')); // Output filename/path for the median ODE parameter combination
	setCfgParam("outGMat", paste0('slim_gmat', identifier, '.csv')); // Output filename/path for the molecular trait G matrices
	setCfgParam("outInd", paste0('slim_indPheno', identifier, '.csv')); // Output filename/path for individual phenotype data
	setCfgParam("outPed", paste0('slim_pedigree', identifier, '.csv')); // Output filepath for pedigree
	setCfgParam("outHaplo", paste0('slim_haplo', identifier, '.csv')); // Output filepath for haplotypes
	setCfgParam("outPhenoSample", paste0('slim_sampled_pheno', identifier, '.csv')); // Output filepath for sampled phenotypes
	setCfgParam("outMolSample", paste0('slim_sampled_moltrait', identifier, '.csv')); // Output filepath for sampled molecular trait values
	setCfgParam("outGenMap", paste0('slim_genmap', identifier, '.csv')); // Output filepath for genetic map
	setCfgParam("outRel", paste0('slim_rel', identifier, '.csv')); // Output filepath for relatedness matrix
	setCfgParam("outFX", paste0('slim_fx', identifier, '.csv')); // Output filepath for effect sizes associated with haplotypes
	setCfgParam("outPopState", paste0('slim_popstate', identifier, '.bin')); // Output binary population saved state
	setCfgParam("outMutsInd", paste0('slim_indMut', identifier, '.csv')); // Output individual mutation/allele information
	
	
	setCfgParam("moveDir", "/scratch/ht96/nb9894/indTrack/"); // Directory to move output to at end of simulation
	
	// Time tests
	setCfgParam("outTime", paste0('slim_time', identifier, '.csv'));
	
	if (debug == T)
		defineConstant("beginTime", clock());
	
	// Expected values for theta and heterozygosity at mutation-drift equilibrium	
	defineConstant("expTheta", 4*N*mu);
	defineConstant("expHe", expTheta/(1+expTheta));
	
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees=T);
	//	initializeTreeSeq();
	initializeMutationRate(mu);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	
	// m2 mutation type: background
	initializeMutationType("m2", 0.5, "n", 0.0, 1.0);
	m2.color = "red";

	//m3 mutation type: aZ mutation
	initializeMutationType("m3", 0.5, "s", locidist);
	m3.color = "green";
	
	//m4 mutation type: bZ mutation
	initializeMutationType("m4", 0.5, "s", locidist);
	m4.color = "blue";
	
	//m5 mutation type: KZ mutation
	initializeMutationType("m5", 0.5, "s", locidist);
	m5.color = "cyan";
	
	//m6 mutation type: KXZ mutation
	initializeMutationType("m6", 0.5, "s", locidist);
	m6.color = "purple";
	
	
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 kb with uniform recombination
	initializeGenomicElement(g1, 0, genomelength-1);
	initializeRecombinationRate(rwide);

}

mutation(m1) {
	// Set the new mutation type according to a randomly sampled mutation type that is available at a certain site
	mut.setMutationType(sampleMutTypeFromSite(mut.position));
	mut.setSelectionCoeff(mut.mutationType.drawSelectionCoefficient());
	
	if (molTraitFix == -1)
		return T;
	
	// Check if the mutation type is meant to be locked: if it is, don't mutate	
	if (mut.mutationType == sim.mutationTypes[molTraitFix+2])
		return F;
	
	
	return T;
}

fitness(m2) {
	return 1.0;
}

fitness(m3) {
	return 1.0;
}
fitness(m4) {
	return 1.0;
}
fitness(m5) {
	return 1.0;
}
fitness(m6) {
	return 1.0;
}

1 {
	sim.addSubpop("p1", N);
	sim.addSubpop("p2", 0); // Staging population for crossing experiment
	assignQTLPositions();
	
	sim.readFromPopulationFile(popPath);
}

// Burn-in under stabilising selection
s3 reproduction() {
	
	// Mating design at end of burn-in
	if ( (sim.generation) == burnTime ) {
		sires = p1.sampleIndividuals(N_sires);
		for (sire in sires) {
			dams = p1.sampleIndividuals(N_dam, exclude = sire);
			for (dam in dams) {
				for (offspring in seqLen(N_perdam)) {
					p2.addCrossed(sire, dam);
				}
			}
		}
	}
	
	inds = p1.individuals;
	fitness = sim.getValue("fitness");
	parent1 = sample(inds, N, replace = T, weights = fitness);
	parent2 = sample(inds, N, replace = T, weights = fitness);
	
	for (i in seqLen(N))
		p1.addCrossed(parent1[i], parent2[i]);
	
	self.active = 0;
}
// full-sib half-sib mating design: we want to keep variability around though,
// so we still sample the rest of the population according to the regular rules
s4 reproduction() {
	// Only do the mating design a generation before we sample, add individuals to the staging population
	if (!((sim.generation) % testSampling) | (sim.generation) == (burnTime+testTime)) {
		sires = p1.sampleIndividuals(N_sires);
		for (sire in sires) {
			dams = p1.sampleIndividuals(N_dam, exclude = sire);
			for (dam in dams) {
				for (offspring in seqLen(N_perdam)) {
					p2.addCrossed(sire, dam);
				}
			}
		}
		// Calc phenotypes
		// calcPhenotype(p2.individuals);
	}
	
	inds = p1.individuals;
	fitness = sim.getValue("fitness");
	parent1 = sample(inds, N, replace = T, weights = fitness);
	parent2 = sample(inds, N, replace = T, weights = fitness);
	
	for (i in seqLen(N))
		p1.addCrossed(parent1[i], parent2[i]);
	
	self.active = 0;
}

survival() {
	return (individual.age == 0);
}


// output all fixed mutations at end
2000 late() { sim.outputFixedMutations(); }
