// set up a simple neutral simulation
initialize() {
	if (!exists("slimgui")) {
		defineConstant("seed", getSeed());
	}
	else {
		setSeed(asInteger(round(runif(1, 1, 2^62 - 1))));
		defineConstant("seed", getSeed());
		catn("Actual seed: " + asInteger(seed));
	}
	
	
	defineConstant("wd", "/home/564/nb9894/tests/standingVar/mutVar/slim/"); // "/mnt/c/GitHub/SLiMTests/tests/standingVar/mutVar/slim/"); // 
	// Load functions from file
	source(paste0(wd, "baseFns.slim"));
	
	setCfgParam("debug", F);
	setCfgParam("inputPath", "/g/data/ht96/nb9894/standingVar/adjustedTau/popstates/"); // "/mnt/c/GitHub/SLiMTests/tests/standingVar/mutVar/"); // input population file to load
	setCfgParam("inputSeed", 800260023);
	setCfgParam("inputModel", 419);
	setCfgParam("inputID", paste0(inputSeed, "_", inputModel));
	setCfgParam("modelType", "ODE"); // The model type
	setCfgParam("mu", 1e-5); // Per locus mutation rate
	setCfgParam("N", 5000); // Population size
	setCfgParam("N_replicates", 3); // Number of replicate sampled genotypes to measure Vm from
	setCfgParam("N_p2", 100000); // Population size for the cloned population
	setCfgParam("del_mean", -1); // Mean used for the gamma distribution that deleterious mutations are pulled from
	setCfgParam("del_shape", 10); // Shape parameter for gamma distribution used for deleterious mutations
	setCfgParam("rwide", 0.0); // Whole genome recombination rate - set to zero
	setCfgParam("nloci", 1024); // Number of U and E sites along the chromosome
	setCfgParam("molMutProp", c(1/4, 1/4, 1/4, 1/4)); // proportion of molecular trait loci
	setCfgParam("genomelength", 1024); // Loci across the genome - some are deleterious, others QTL, others neutral
	setCfgParam("locimu", 0.0); // Mean used for QTL mutation effect distribution pulls
	setCfgParam("locisigma", 0.0125); // Variance used for QTL effect pulls - the effect size distribution
	//setCfgParam("locidist", paste0("exp(rnorm(1,", locimu, ",", locisigma, "));"));
	setCfgParam("con_props", c(1.0, 0.0, 0.0)); // Proportion of genes that have low/medium/high substitution rate
	setCfgParam("constraints", matrix(c(1.0, 0.0, 1.0, 1.0, 1.0, 1.0,
		5.0, 10.0, 1.0, 1.0, 1.0, 1.0,
		50.0, 100.0, 1.0, 1.0, 1.0, 1.0),
		ncol = 6, byrow = T)); // Matrix of lambdas: m1/m2/m3/m4/m5/m6 weights in columns, rows are low/medium/high sub rate
	
	setCfgParam("width", 0.05);	// Set so 1 phenotypic unit deviation is a 20% drop in fitness
	// TODO: Set this to a large value, adjust width also to have a longer adaptation period: measure the distribution of effects for adaptation!!!!	
	setCfgParam("molTraitFix", c(2,3)); // Molecular trait to hold fixed (disable mutations): 0 = aZ, 1 = bZ, 2 = KZ, 3 = KXZ, -1: none
	setCfgParam("lambda", (locisigma * ifelse(any(molTraitFix != -1), 2, 4) / 123.67)); // Scaling constant for the effect size distribution of each mol comp

	setCfgParam("modelindex", 1); // Identifier for the combination of predictors used in latin hypercube: this is the row number 
	// in the lscombos.csv file
	setCfgParam("identifier", paste(asString(seed), modelindex, sep = "_")); // Unique identifier for the run - seed_modelindex
	
	// Set up the filesystem for saving data - thread safety, write to separate files for each run with unique names
	setCfgParam("outMutVar", c(paste0('slim_mutvar_adjtau', identifier, '.csv'))); // Output filename/path for shared mutation frequencies
	setCfgParam("outMutVarPerComp", c(paste0('slim_mutvar_percomp_adjtau', identifier, '.csv'))); // Output filename/path for mutational variance per mol comp
	setCfgParam("moveDir", "/scratch/ht96/nb9894/standingVar/mutVar/"); // Directory to move output to at end of simulation
				
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees=T);
	initializeMutationRate(mu);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	m1.convertToSubstitution = T;
	
	// m2 mutation type: background
	initializeMutationType("m2", 0.5, "n", 0.0, 1.0);
	m2.color = "red";
	m1.convertToSubstitution = T;
	
	//m3 mutation type: aZ mutation
	initializeMutationType("m3", 0.5, "n", locimu, lambda * 0.0865);
	m3.color = "green";
	m3.convertToSubstitution = T;
	
	//m4 mutation type: bZ mutation
	initializeMutationType("m4", 0.5, "n", locimu, lambda * 0.1189);
	m4.color = "blue";
	m4.convertToSubstitution = T;
	
	//m5 mutation type: KZ mutation
	initializeMutationType("m5", 0.5, "n", locimu, lambda * 123.457);
	m5.color = "cyan";
	m5.convertToSubstitution = T;
	
	//m6 mutation type: KXZ mutation
	initializeMutationType("m6", 0.5, "n", locimu, lambda * 0.00743);
	m6.color = "purple";
	m6.convertToSubstitution = T;
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 kb with uniform recombination
	initializeGenomicElement(g1, 0, genomelength-1);
	initializeRecombinationRate(rwide);
}

mutation(m1, p3) {
	//if (subpop == p1) { return F; }
	
	// Set the new mutation type according to a randomly sampled mutation type that is available at a certain site
	mut.setMutationType(sampleMutTypeFromSite(mut.position));
	mut.setSelectionCoeff(mut.mutationType.drawSelectionCoefficient());
	
	if (all(molTraitFix == -1))
		return T;
	
	// Check if the mutation type is meant to be locked: if it is, don't mutate	
	if (any(mut.mutationType == sim.mutationTypes[molTraitFix+2])) {
		return F;
	}
	
	
	return T;
}

mutationEffect(m2) {
	return 1.0;
}

mutationEffect(m3) {
	return 1.0;
}
mutationEffect(m4) {
	return 1.0;
}
mutationEffect(m5) {
	return 1.0;
}
mutationEffect(m6) {
	return 1.0;
}

// Setup population
1 early() {
	LoadPopulation();
	
	// Remove p2
	if (exists("p2")) {
		p2.removeSubpopulation();
	}
		sim.addSubpop("p3", 0);
		
   // Sample N_replicates genotypes to calculate mutations from
	community.rescheduleScriptBlock(s1, start = sim.cycle + 1, end = sim.cycle + N_replicates * 2);
	defineGlobal("cur_index", 0);
	// Diagnostics for this run
	catn(paste0("Running modelindex: ", modelindex, "; seed: ", asString(seed)));

}

// handle cloning the population
s1 late() {
	if (cur_index % 2 != 0) {
		// Measure variance
		calcPhenotype(p3.individuals, modelType);
		phenotypes = p3.individuals.phenotype;
		phenovar = var(phenotypes);

		// Covariance of phenotypes with each molecular component
		molComps = p3.individuals.ODEPars;
		aZ = molComps[seq(1, length(molComps)-1, by = 5)];
		bZ = molComps[seq(2, length(molComps)-1, by = 5)];
		KZ = molComps[seq(3, length(molComps)-1, by = 5)];
		KXZ = molComps[seq(4, length(molComps)-1, by = 5)];

		cov_aZ = cov(phenotypes, aZ);
		cov_bZ = cov(phenotypes, bZ);
		cov_KZ = cov(phenotypes, KZ);
		cov_KXZ = cov(phenotypes, KXZ);

		// Write output
		WriteMutVarOutput(phenovar);
		WriteMutVarPerMolComp(cov_aZ, cov_bZ, cov_KZ, cov_KXZ);
	} 
	
	cur_index = cur_index + 1;

	// Move output
	if (cur_index == N_replicates * 2) {
		system(paste0("mv ./", outMutVar, " ", moveDir));
		system(paste0("mv ./", outMutVarPerComp, " ", moveDir));
	}
}


// full-sib half-sib mating design: we want to keep variability around though,
// so we still sample the rest of the population according to the regular rules
reproduction(p1) {	
	// Allow mutations to accrue every other generation
	if (cur_index % 2 != 0) {
		self.active = 0;
		return;
	} 
	
	// Create a new sample from p1
	if (cur_index % 2 == 0) {
		// Clone p2 from ind	
		ind = p1.sampleIndividuals(1);
		p3.addCloned(ind, N_p2);
		self.active = 0;
	}
}

reproduction(p3) {
	if (cur_index % 2 != 0) {
		// Clone p3 from ind	
		p3.addCloned(individual, 1);
		return;
	}
	
	self.active = 0;
	return;
}

survival(p1) {
	return T;
}

survival(p3) {
	return (individual.age < 1);
}

function (void) WriteMutVarOutput(f$ variance) {
	Mfile = paste(cur_index, asString(seed), modelindex, variance, sep=",");
	writeFile(outMutVar, Mfile, append = T);
}

function (void) WriteMutVarPerMolComp(f$ cov_aZ, f$ cov_bZ, f$ cov_KZ, f$ cov_KXZ) {
	Mfile = paste(cur_index, asString(seed), modelindex, cov_aZ, cov_bZ, cov_KZ, cov_KXZ, sep=",");
	writeFile(outMutVarPerComp, Mfile, append = T);
}


function (void) LoadPopulation(void) {
	sim.readFromPopulationFile(paste0(inputPath, "slim_popstate", inputID, ".bin"));
	
	// QTL positions
	pos = readCSV(paste0("./slim_pos", inputID, ".csv"), colNames = F);
	pos_QTL = c(pos.subsetColumns(3:(pos.ncol-1)).asMatrix());
	sim.setValue("pos_QTL", pos_QTL);
	
	// Constraint dictionary
	dict = readCSV(paste0("./slim_dict", inputID, ".csv"), colNames = F);
	dict_genes = c(dict.subsetColumns(2:(genomelength+1)).asMatrix());
	dict_constraint = c(dict.subsetColumns((genomelength+3):(dict.ncol-1)).asMatrix());
	con_dict = matrix(c(dict_genes, dict_constraint), ncol = genomelength, byrow = T);	
	sim.setValue("con_dict", con_dict);
	
	// Optimum
	opt = readCSV(paste0("./slim_opt", inputID, ".csv"), colNames = F);
	opt = opt.subset(0, 2);
	
	sim.setValue("optimum", opt);
	
	// Misc initial values

	// Set delta difference value
	sim.setValue("lastDist", 0.0);
	sim.setValue("lastw", 0.0);
		
	// Set mutation rate
	// Calculate and set mutation rates according to n loci so we have a consistent per-genome rate
	u_g = genomelength * mu;
	u_q = u_g / nloci;
	u_n = u_g / (genomelength - nloci);
	
	// return early if we have no QTLs: mutation rate is always mu
	if (length(pos_QTL) == 0) {
		return;
	}
	
	// Set molMutCutoffs
	// mol traits are in order: first molMutProp[0]% are alpha, next molMutProp[1]% are beta, then KZ, then KXZ
	// figure out the actual cutoffs 
	molMutres = integer(size(molMutProp));

	for (i in seqLen(size(molMutProp)))
	{
		if (i == 0) 
		{ 
			molMutres[i] = asInteger(ceil(molMutProp[i] * nloci)); 
			next;
		}

		if (i == size(molMutProp)-1) 
		{ 
			molMutres[i] = nloci; 
			next;
		}

		molMutres[i] = asInteger(ceil(molMutProp[i] * nloci)) + molMutres[i-1];
	}
	
	// if the first element has 0 loci, remove it
	if (molMutres[0] == 0) {
		molMutres = molMutres[1:(length(molMutres)-1)];
	}
		
	// Ensure we don't go over nloci
	molMutres[molMutres > nloci] = nloci; 
		
	// Remove extras in case we don't have all the molecular components (e.g. molMutProp has zeros) 
	molMutres = unique(molMutres);

	defineConstant("molMutCutoffs", c(0, molMutres-1)); // convert to zero-indexed
	
	ends = unique(findMutBreakpoints(pos_QTL));
	
	

	// determine what rates to use: starting with neutral or qtl depending on whether we have a QTL at pos 0
	if (sort(pos_QTL)[0] == 0) {
		rates = rep(c(u_q, u_n), length(ends));
	} else {
		rates = rep(c(u_n, u_q), length(ends));
	}
	
	// shorten to match ends length
	rates = rates[0:(length(ends)-1)];	
		
	// get a vector of values
	sim.chromosome.setMutationRate(rates, ends);	
	
	// Calculate p2 phenotypes first so we don't overwrite p1 sim.setvalue("fitness")
	calcPhenotype(p2.individuals, modelType);
	calcPhenotype(p1.individuals, modelType);
	calcFitnessGaussian(p1.individuals, opt);
	
	setSeed(inputSeed);
	return;
}
